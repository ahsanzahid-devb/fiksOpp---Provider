buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.1'
        classpath 'com.google.gms:google-services:4.3.14'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://phonepe.mycloudrepo.io/public/repositories/phonepe-intentsdk-android"
        }
    }
}

ext {
    compileSdkVersion = 35
    targetSdkVersion = 35
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    afterEvaluate {
        // Skip the app module - it has its own compileSdk setting
        if (project.name != 'app' && it.hasProperty('android')) {
            it.android {
                if (namespace == null) {
                    namespace it.group
                }
                // Set compileSdk to 35 for all plugins (minimum required by some dependencies)
                compileSdk 35
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
